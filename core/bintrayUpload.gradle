apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

//load & set developer info
Properties properties = new Properties()
File localPropertiesFile = project.file("../../PUBLISH/bintray.properties")
if (localPropertiesFile.exists()) {
    properties.load(localPropertiesFile.newDataInputStream())
}
def developerId = properties.getProperty("developer.id")
def developerName = properties.getProperty("developer.name")
def developerEmail = properties.getProperty("developer.email")
def bintrayUser = properties.getProperty("bintray.user")
def bintrayApikey = properties.getProperty("bintray.apiKey")


ext {
    packagingType = 'aar'
    packageInceptionYear = 2018

    libraryGroup = 'org.pengyr.tool'
    libraryName = 'tool-core'
    libraryDescription = 'Core tool of peng\'s tools'

    siteUrl = 'https://github.com/peng571/tool-core'
    gitUrl = 'https://github.com/peng571/tool-core.git'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'

    versionName = android.defaultConfig.versionName
    versionDesc = "first update to jCenter"
}

//create sources.jar
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}


task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

//create javadoc.jar 
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}


tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

//javadoc options 
javadoc {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version versionName
        links "http://docs.oracle.com/javase/7/docs/api"
        title "core tool api doc"
    }
}


group = libraryGroup
version = versionName
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                name libraryDescription
                url siteUrl
                inceptionYear packageInceptionYear

                packaging packagingType
                groupId libraryGroup
                artifactId libraryName
                version versionName

                licenses  {
                    license {
                        name licenseName
                        url licenseUrl
                    }
                }
                developers {
                    developer {
                        id developerId
                        name developerName
                        email developerEmail
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}


bintray {
    user = bintrayUser
    key = bintrayApikey
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = libraryName
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
        version {
            name = versionName
            desc = versionDesc
            released = new Date()
        }
    }
}

 